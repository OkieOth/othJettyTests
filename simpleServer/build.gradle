apply plugin: 'java'
//apply plugin: 'application'
apply plugin: 'groovy'
apply plugin: 'maven-publish'

sourceCompatibility = '1.7'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

project.group = 'de.othsoft.examples.jetty'
project.version = '0.1'


// NetBeans will automatically add "run" and "debug" tasks relying on the
// "mainClass" property. You may however define the property prior executing
// tasks by passing a "-PmainClass=<QUALIFIED_CLASS_NAME>" argument.
//
// Note however, that you may define your own "run" and "debug" task if you
// prefer. In this case NetBeans will not add these tasks but you may rely on
// your own implementation.

if (!hasProperty('mainClass')) {
    ext.mainClass = 'de.othsoft.examples.jetty.SimpleServer'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

publishing {
    repositories {
        maven {
            url "${System.getenv('HOME')}/myMavenRepos"
        }
    }
}

repositories {
    maven {
        url "${System.getenv('HOME')}/myMavenRepos"
    }
    mavenCentral()
}

dependencies {
    compile 'de.othsoft.helper:othJettyHelper:0.1'
    testCompile 'org.codehaus.groovy:groovy-all:2.4.3'
    testCompile 'junit:junit:4.10'
    testCompile 'de.othsoft.helper:othJettyHelper:0.1:test'
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration_test/java')
        }
        groovy {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration_test/groovy')            
        }
        /* not needed this time
        resources.srcDir file('src/integration-test/resources')
        */
    }
}

task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'Jetty Test Simple Server',  
        	'Implementation-Version': version,
        	'Main-Class': 'de.othsoft.examples.jetty.SimpleServer'
    }
    baseName = project.name + '-all'
    // TODO - if the file exists remove it
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

task copyFatJar(type: Copy) {
    def fatJarName = "${project.name}-all-${project.version}.jar"
    outputs.files file("${project.rootDir}/build/libs/$fatJarName")
    println "copy fatJar '${fatJarName}' from '${project.rootDir}/build/libs' to '${project.rootDir}/src/integration_test/vagrant/ubuntu_logserver/tmp'"
    copy {
        from "${project.rootDir}/build/libs"
        into "${project.rootDir}/src/integration_test/vagrant/ubuntu_logserver/tmp"
        include fatJarName
    }    
}

task integrationTest(type: Test) {
    doFirst {
        // that's the place to start the test vagrant machine        
        println 'before integration tests - ' + new Date().getTime()
        println 'start vm'
        "${project.rootDir}/src/integration_test/scripts/startVagrant.sh".execute().in.eachLine {
            println it
        }
        println 'vm started'
        println 'retrieve the ip address of the vm'
       "${project.rootDir}/src/integration_test/scripts/print_vm_address.sh".execute().in.eachLine {
            println it
       }
    }
    
    doLast {
        // that's the place to start the test vagrant machine
        def sout = new StringBuffer()
        def serr = new StringBuffer()
        "${project.rootDir}/src/integration_test/scripts/stopVagrant.sh".execute().in.eachLine {
            println it
        }
        println 'after integration tests - ' + new Date().getTime()
    }
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
}

copyFatJar.dependsOn fatJar
copyFatJar.mustRunAfter testClasses
integrationTest.dependsOn copyFatJar

check.dependsOn integrationTest
integrationTest.mustRunAfter test


tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}

task dependenciesToLibDir(type: Copy) {
    into "$buildDir/release/lib"
    from configurations.runtime
}

task printDeps << {
	configurations.runtime.forEach {
		println it
	}
}
